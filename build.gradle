
plugins {
    id "org.sonarqube" version "4.0.0.2929"
}

// Apply the java plugin to add support for Java
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: "jacoco"
apply plugin: 'maven-publish'
apply plugin: 'signing'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

ext {
    version = "0.3.6"
    repoUrl = 'https://github.com/simonpoole/pmtiles-reader.git'
    spdxId = 'BSD 3-Clause'
}

version = project.ext.version

jacocoTestReport {
    reports {
        xml.required=true
    }
}

sonarqube {
    properties {
        property "sonar.java.source","1.8"
        property "sonar.sourceEncoding","UTF-8"
        property "sonar.java.binaries", "build/classes/java/main/"
        property "sonar.coverage.jacoco.xmlReportPaths","build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.qualitygate.wait", "true"
    }
}

task replaceVersion {
    doLast {
        ant.replaceregexp(match:'ch\\.poole\\.geo\\.pmtiles-reader\\:Reader\\:([0-9\\.]+)', replace:"ch.poole.geo.pmtiles-reader:Reader:${project.ext.version}", flags:'g', byline:true) {
            fileset(dir: '.', includes: 'README.md')
        }
    }
}
build.dependsOn replaceVersion

jar {
    manifest {
        attributes("Automatic-Module-Name": "ch.poole.geo.pmtiles-reader")
    }
}

publishing {
    publications {
        Release(MavenPublication) {
            groupId 'ch.poole.geo.pmtiles-reader'
            artifactId 'Reader'
            from components.java
            pom {
                name = artifactId
                description = 'This is a simple reader for PMTiles format tile storage' 
                url = 'https://github.com/simonpoole/pmtiles-reader'
                scm {
                    url = repoUrl
                }
                licenses {
                    license {
                        name = spdxId
                        url = 'https://raw.githubusercontent.com/simonpoole/pmtiles-reader/master/LICENCE.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Simon Poole'
                    }
                }
            }
            repositories {
                maven {
                    name = 'sonatype'
                    credentials {
                        username System.getenv('SONATYPE_USER')
                        password System.getenv('SONATYPE_PASSWORD')
                    }
                    def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
        }
    }
}

signing {
    sign publishing.publications.Release
}


repositories {
    mavenCentral()
    mavenLocal()
}


dependencies {   
    implementation 'org.jetbrains:annotations:24.0.1'
    testImplementation 'org.jetbrains:annotations:24.0.1'
    testImplementation 'junit:junit:4.12'
    testImplementation "com.squareup.okhttp3:mockwebserver:3.12.13"
}
